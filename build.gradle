// Gradle script to build the jme3-utilities project

import org.gradle.api.artifacts.*

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    group = 'com.github.stephengold'
    websiteUrl = 'https://github.com/stephengold/jme3-utilities'

    // module coordinates of external dependencies:
    acorusCoordinates = 'com.github.stephengold:Acorus:2.0.0'
    heartCoordinates = 'com.github.stephengold:Heart:9.0.0'

    // current versions of libraries:
    jme3utilitiesniftyVersion = '0.9.38-SNAPSHOT'
    jme3utilitiesxVersion = '0.3.3-SNAPSHOT'
    jme3Version = '3.7.0-beta1'
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('checkstyle') {
    dependsOn ':nifty:checkstyleMain', ':tests:checkstyleMain', ':x:checkstyleMain'
    description 'Checks the style of all Java sourcecode.'
}

def mergedJavadocSubprojects = [ ":nifty", ":x" ]
tasks.register('mergedJavadoc', Javadoc) {
    description 'Creates Javadoc from all the projects.'
    destinationDir = new File(project.buildDir, 'merged-javadoc')
    title = 'All modules'

    source = mergedJavadocSubprojects.collect { project(it).sourceSets.main.allJava }
    classpath = files(mergedJavadocSubprojects.collect { project(it).sourceSets.main.compileClasspath })
}
