// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application'
}

dependencies {
    implementation 'org.apache.commons:commons-exec:1.3'

    implementation heartCoordinates
    implementation acorusCoordinates
    runtimeOnly 'com.github.nifty-gui:nifty-style-black:1.4.3'
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version

    // "tests" doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jme3Version

    //implementation "com.github.stephengold:jme3-utilities-nifty:$jme3utilitiesniftyVersion" // for published library
    implementation project(':nifty') // for latest sourcecode

    //implementation "com.github.stephengold:jme3-utilities-x:$jme3utilitiesxVersion" // for published library
    implementation project(':x') // for latest sourcecode
}

startScripts.dependsOn(':nifty:build')

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSandbox', 'cleanSOs')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}

// tasks to run the test apps:

tasks.register('TestPolygon3f', JavaExec) {
    mainClass = 'jme3utilities.math.test.TestPolygon3f'
}
tasks.register('TestMyShell3f', JavaExec) {
    mainClass = 'jme3utilities.math.test.TestShell3f'
}

tasks.register('AppChooser', JavaExec) {
    mainClass = 'jme3utilities.nifty.test.AppChooser'
}

tasks.register('ClockDemo', JavaExec) {
    mainClass = 'jme3utilities.nifty.test.ClockDemo'
}
tasks.register('TestBindScreen', JavaExec) {
    mainClass = 'jme3utilities.nifty.test.TestBindScreen'
}
tasks.register('TestMultipleScreens', JavaExec) {
    mainClass = 'jme3utilities.nifty.test.TestMultipleScreens'
}
tasks.register('TestMultipleWindows', JavaExec) {
    mainClass = 'jme3utilities.nifty.test.TestMultipleWindows'
}
tasks.register('TestPopups', JavaExec) {
    mainClass = 'jme3utilities.nifty.test.popups.TestPopups'
}