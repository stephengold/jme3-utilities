// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

dependencies {
    runtimeOnly 'com.github.nifty-gui:nifty-style-black:1.4.3'
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version

    // "tests" doesn't use jme3-jogg nor jme3-plugins
    //  -- they are included solely to avoid warnings from AssetConfig
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jme3Version

    implementation heartCoordinates
    implementation acorusCoordinates

    //implementation "com.github.stephengold:jme3-utilities-nifty:$jme3utilitiesniftyVersion"
    implementation project(':nifty')

    //implementation "com.github.stephengold:jme3-utilities-x:$jme3utilitiesxVersion"
    implementation project(':x')
}

// cleanup tasks

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')

task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}

// tasks to run the test apps

task TestPolygon3f(type: JavaExec) {
    mainClass = 'jme3utilities.math.test.TestPolygon3f'
}
task TestMyShell3f(type: JavaExec) {
    mainClass = 'jme3utilities.math.test.TestShell3f'
}

task ClockDemo(type: JavaExec) {
    mainClass = 'jme3utilities.nifty.test.ClockDemo'
}
task runTestBindScreen(type: JavaExec) {
    mainClass = 'jme3utilities.nifty.test.TestBindScreen'
}
task TestMultipleScreens(type: JavaExec) {
    mainClass = 'jme3utilities.nifty.test.TestMultipleScreens'
}
task TestPopups(type: JavaExec) {
    mainClass = 'jme3utilities.nifty.test.TestPopups'
}