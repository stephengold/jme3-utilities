// Gradle settings and tasks common to all jme3-utilities subprojects

apply plugin: 'java'

ext {
    group = 'com.github.stephengold'
    websiteUrl = 'https://github.com/stephengold/jme3-utilities'

    // module coordinates of dependencies:
    acorusCoordinates = 'com.github.stephengold:Acorus:0.9.7'
    heartCoordinates = 'com.github.stephengold:Heart:7.2.0'

    // current versions of libraries:
    jme3utilitiesniftyVersion = '0.9.27-SNAPSHOT'
    jme3utilitiesxVersion = '0.3.1-SNAPSHOT'
    jme3Version = '3.5.0-stable'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) { // Java runtime options:
    classpath sourceSets.main.runtimeClasspath
    //debug true
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xbatch'
    //jvmArgs '-Xms512m', '-Xmx512m'
    //jvmArgs '-XX:+PrintCompilation'
    //jvmArgs '-XX:+UseConcMarkSweepGC'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

repositories {
    //mavenLocal()
    mavenCentral()
}

// custom tasks for creating source/javadoc JARs

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Creates a JAR of javadoc.'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    description 'Creates a JAR of sourcecode.'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.allJava
}
