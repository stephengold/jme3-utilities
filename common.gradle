// Gradle settings and tasks common to all jme3-utilities subprojects

apply plugin: 'checkstyle' // to analyze Java sourcecode for style violations
apply plugin: 'java'       // to compile and test Java projects

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_8
}

checkstyle {
    toolVersion = libs.versions.checkstyle.get()
}

tasks.withType(JavaCompile).configureEach { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'

    // Suppress warnings that source value 7 is obsolete:
    options.compilerArgs << '-Xlint:-options'

    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    options.release = 8
}

tasks.withType(JavaExec).configureEach { // Java runtime options:
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    dependsOn 'installDist'
    enableAssertions = true
    jvmArgs '--enable-native-access=ALL-UNNAMED' // suppress System::load() warning
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xms512m', '-Xmx512m' // to enlarge the Java heap
    //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    description = 'Creates a JAR of Java sourcecode.'
    from sourceSets.main.allJava // default is ".allSource", which includes resources
}