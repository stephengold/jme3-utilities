// Gradle script to build the "moon-ccbysa" subproject of jme3-utilities

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'maven-publish' // to publish artifacts to Maven repositories
}

ext {
    artifact = 'moon-ccbysa'
    version = '0.9.2'
    baseName = "${artifact}-${version}" // for artifacts
}

java {
    withJavadocJar()
}

jar { archiveBaseName = project.ext.baseName }
javadocJar { archiveBaseName = project.ext.baseName }
sourcesJar { archiveBaseName = project.ext.baseName }

build.dependsOn('pom')
tasks.register('pom', Copy) {
    dependsOn 'generatePomFileForMavenPublication'
    description 'Copies the Maven POM to build/libs.'
    from "${buildDir}/publications/maven/pom-default.xml"
    into "${buildDir}/libs"
    rename 'pom-default.xml', baseName + '.pom'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact sourcesJar
            artifactId artifact
            from components.java
            groupId rootProject.ext.group
            pom {
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'New BSD (3-clause) License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
            }
            version project.ext.version
        }
    }
}