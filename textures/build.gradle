// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

description = 'generate texture assets used by jme3-utilities-debug and SkyControl'
ext {
    bsc = 'src/main/resources/bsc5.dat'
    shapes = '../debug/src/main/resources/Textures/shapes'
    skies = '../SkyControl/src/main/resources/Textures/skies'
}

dependencies {
    compile "com.beust:jcommander:$jcommanderVersion"

    //compile "jme3utilities:jme3-utilities-heart:$jme3utilitiesheartVersion"
    compile project(':heart')
}

clean {
    dependsOn 'cleanCatalog'
    dependsOn 'cleanCopySource'
    dependsOn 'cleanDownload'
    dependsOn 'cleanShapeTextures'
    dependsOn 'cleanSkyTextures'
}
compileJava { dependsOn 'copySource' }

// decompress the Yale Bright Star Catalog

task catalog {
    dependsOn 'download'
    doLast { file(bsc).text = resources.gzip("${bsc}.gz").read().text }
}
task cleanCatalog(type: Delete) {
    delete file(bsc)
}

task copySource(type: Copy) {
    from('../SkyControl/src/main/java/jme3utilities') {
        include 'sky/Constants.java', 'sky/DomeMesh.java', 'sky/LunarPhase.java'
    }
    into 'src/main/java/jme3utilities'
}
task cleanCopySource(type: Delete) {
    delete fileTree(dir: 'src/main/java', exclude: '**/textures/**')
}

// download gzipped Yale Bright Star Catalog

task download(type: MyDownload) {
    sourceUrl = 'http://tdc-www.harvard.edu/catalogs/bsc5.dat.gz'
    target = file("${bsc}.gz")
}
task cleanDownload(type: Delete) {
    delete file("${bsc}.gz")
}

// generate shape textures

task shapeTextures {
    dependsOn = ['cross', 'lozenge', 'mascle', 'ring', \
        'saltire', 'solidCircle', 'square']
    description 'generate texture assets distributed with jme3-utilities-debug'
}
task cleanShapeTextures(type:Delete) {
    delete fileTree(dir: shapes)
}

task cross(type: JavaExec) {
    main 'jme3utilities.debug.textures.MakeCross'
    outputs.files(["$shapes/cross.png"])
}
task lozenge(type: JavaExec) {
    main 'jme3utilities.debug.textures.MakeLozenge'
    outputs.files(["$shapes/lozenge.png"])
}
task mascle(type: JavaExec) {
    main 'jme3utilities.debug.textures.MakeMascle'
    outputs.files(["$shapes/mascle.png"])
}
task ring(type: JavaExec) {
    main 'jme3utilities.debug.textures.MakeRing'
    outputs.files(["$shapes/ring.png"])
}
task saltire(type: JavaExec) {
    main 'jme3utilities.debug.textures.MakeSaltire'
    outputs.files(["$shapes/saltire.png"])
}
task solidCircle(type: JavaExec) {
    main 'jme3utilities.debug.textures.MakeSolidCircle'
    outputs.files(["$shapes/solid circle.png"])
}
task square(type: JavaExec) {
    main 'jme3utilities.debug.textures.MakeSquare'
    outputs.files(["$shapes/square.png"])
}

// generate sky textures

task skyTextures {
    dependsOn = ['clouds', 'equator', 'equator16m', 'moons', 'north', \
        'north16m',  'ramps', 'south', 'south16m', 'suns', 'wiltshire', \
        'wiltshire16m']
    description 'generate texture assets distributed with SkyControl'
}
task cleanSkyTextures(type:Delete) {
    delete fileTree(dir: skies)
}

task clouds(type: JavaExec) {
    main 'jme3utilities.sky.textures.MakeClouds'
    outputs.files(["$skies/clouds/clear.png", \
                   "$skies/clouds/fbm.png", \
                   "$skies/clouds/overcast.png"])
}
task debugEquator(type: JavaExec) {
    args = ['-c', '-p', 'equator']
    debug true
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
}
task equator(type: JavaExec) {
    args = ['-c', '-p', 'equator']
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files(fileTree("$skies/star-maps/equator"))
}
task equator16m(type: JavaExec) {
    args = ['-c', '-p', 'equator_16m']
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files(fileTree("$skies/star-maps/equator16m"))
}
task moons(type: JavaExec) {
    main 'jme3utilities.sky.textures.MakeMoons'
    outputs.files(fileTree("$skies/moon-nonviral"))
}
task north(type: JavaExec) {
    args = ['-p', 'north']
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("$skies/star-maps/northern.png")
}
task north16m(type: JavaExec) {
    args = ['-p', 'north_16m']
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("$skies/star-maps/16m/northern.png")
}
task ramps(type: JavaExec) {
    main 'jme3utilities.sky.textures.MakeRamps'
    outputs.files("$skies/ramps/haze.png")
}
task south(type: JavaExec) {
    args = ['-p', 'south']
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("$skies/star-maps/southern.png")
}
task south16m(type: JavaExec) {
    args = ['-p', 'south_16m']
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("$skies/star-maps/16m/southern.png")
}
task suns(type: JavaExec) {
    main 'jme3utilities.sky.textures.MakeSun'
    outputs.files(fileTree("$skies/suns"))
}
task wiltshire(type: JavaExec) {
    args = ['-p', 'wiltshire']
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("$skies/star-maps/wiltshire.png")
}
task wiltshire16m(type: JavaExec) {
    args = ['-p', 'wiltshire_16m']
    dependsOn catalog
    inputs.files(bsc)
    main 'jme3utilities.sky.textures.MakeStarMaps'
    outputs.files("$skies/star-maps/16m/wiltshire.png")
}

// Helper class to wrap Ant download task
class MyDownload extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}