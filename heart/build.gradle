// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'java-library'
    id 'maven'
}

description = 'core utilities library for jMonkeyEngine'
ext {
    artifact = 'jme3-utilities-heart'
    version = jme3utilitiesheartVersion
    baseName = "$artifact-$version"
}

build.dependsOn('pom')
jar {
    baseName project.ext.baseName
    exclude('**/arrow.blend')
}
javadocJar { baseName project.ext.baseName }
sourcesJar { baseName project.ext.baseName }
processResources.dependsOn(':textures:shapeTextures')

dependencies {
    api "org.jmonkeyengine:jme3-core:$jmonkeyengineVersion"
    testImplementation 'junit:junit:4.12'
}

task pom {
    doLast {
        pom {
            project {
                artifactId project.ext.artifact
                licenses {
                    license {
                        distribution 'repo'
                        name 'New BSD (3-clause) License'
                        url 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                version project.ext.version
            }
        }.writeTo("${buildDir}/libs/${project.ext.baseName}.pom")
    }
}
