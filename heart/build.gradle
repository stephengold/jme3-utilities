// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'java-library'
    id 'maven-publish'
}

description = 'core utilities library for jMonkeyEngine'
ext {
    artifact = 'jme3-utilities-heart'
    version = jme3utilitiesheartVersion
    baseName = "$artifact-$version"
}

jar {
    archiveBaseName = project.ext.baseName
    exclude('**/arrow.blend')
}
javadocJar { archiveBaseName = project.ext.baseName }
sourcesJar { archiveBaseName = project.ext.baseName }
processResources.dependsOn(':textures:shapeTextures')

dependencies {
    api "org.jmonkeyengine:jme3-core:$jmonkeyengineVersion"
    testImplementation 'junit:junit:4.12'
}

build.dependsOn('pom')
task('pom', type: Copy) {
    dependsOn 'generatePomFileForMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml"
    into "${buildDir}/libs"
    rename '.*', baseName + '.pom'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId artifact
            from components.java
            pom {
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'New BSD (3-clause) License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
            }
            version project.ext.version
        }
    }
}